#################################################################################
#
# This script extracts F1, F2, F3, and duration from all phonemes in a Sound file.
# It assumes phonemes are on Tier 1 and words are on Tier 2.
# It outputs this information to /Users/joeystanley/Desktop/Projects/Praat/formants.csv
#
# To run this script, open a Sound and TextGrid in Praat and have them selected.
#
# This script was created by following the workshop by Joey Stanley & Lisa Lipani:
# http://joeystanley.com/downloads/191002-formant_extraction.html
# 
# Joey Stanley
# Tuesday, October 1, 2019
# Main Libary, UGA campus, Athens, GA, USA
#
#################################################################################

writeInfoLine: "Extracting formants..."

# Extract the names of the Praat objects
thisSound$ = selected$("Sound")
thisTextGrid$ = selected$("TextGrid")

# Extract the number of intervals in the phoneme tier
select TextGrid 'thisTextGrid$'
numberOfPhonemes = Get number of intervals: 1  
appendInfoLine: "There are ", numberOfPhonemes, " intervals."

# Create the Formant Object
select Sound 'thisSound$'
To Formant (burg)... 0 5 5000 0.025 50

# Create the output file and write the first line.
outputPath$ = "test_set/results/pitch_info/subject1_formants.csv"
writeFileLine: "'outputPath$'", "file,time,word,phoneme,duration,F1,F2,F3,p_mean,p_min,p_max,p_stdev"

select Sound 'thisSound$'
To Pitch... 0 75 600

# Loop through each interval on the phoneme tier.
for thisInterval from 1 to numberOfPhonemes
    #appendInfoLine: thisInterval

    # Get the label of the interval
    select TextGrid 'thisTextGrid$'
    thisPhoneme$ = Get label of interval: 1, thisInterval
    #appendInfoLine: thisPhoneme$
      
    # Find the midpoint.
    thisPhonemeStartTime = Get start point: 1, thisInterval
    thisPhonemeEndTime   = Get end point:   1, thisInterval
    duration = thisPhonemeEndTime - thisPhonemeStartTime
    midpoint = thisPhonemeStartTime + duration/2
    
    # Get the word interval and then the label
    thisWordInterval = Get interval at time: 2, midpoint
    thisWord$ = Get label of interval: 2, thisWordInterval
      
    # Extract formant measurements
    select Formant 'thisSound$'
    f1 = Get value at time... 1 midpoint Hertz Linear
    f2 = Get value at time... 2 midpoint Hertz Linear
    f3 = Get value at time... 3 midpoint Hertz Linear

    # Extract pitch measurements
    select Pitch 'thisSound$'
    p_mean = Get mean: thisPhonemeStartTime, thisPhonemeEndTime, "Hertz"
    p_min = Get minimum: thisPhonemeStartTime, thisPhonemeEndTime, "Hertz", "Parabolic"
    p_max = Get maximum: thisPhonemeStartTime, thisPhonemeEndTime, "Hertz", "Parabolic"
    p_stdev = Get standard deviation: thisPhonemeStartTime, thisPhonemeEndTime, "Hertz"

    # Save to a spreadsheet
    appendFileLine: "'outputPath$'", 
                      ...thisSound$, ",",
                      ...midpoint, ",",
                      ...thisWord$, ",",
                      ...thisPhoneme$, ",",
                      ...duration, ",",
                      ...f1, ",", 
                      ...f2, ",", 
                      ...f3, ",", 
                      ...p_mean, ",", 
                      ...p_min, ",", 
                      ...p_max, ",", 
                      ...p_stdev

endfor

appendInfoLine: newline$, newline$, "Whoo-hoo! It didn't crash!"